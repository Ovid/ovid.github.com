#!/usr/bin/env perl

# vim: ft=perl
use lib 'lib';

use DateTime;
use DateTime::Format::SQLite;
use XML::RSS;
use Less::Script;
use Less::Pager;

use HTML::TokeParser::Simple;
use Capture::Tiny 'capture';

use File::Find::Rule;
use File::Spec::Functions qw(catfile catdir);
use File::Basename qw(dirname basename);
use File::Path qw(mkpath);
use File::Copy;

my @files = File::Find::Rule->file->in('root');

preprocess_files(@files);
rebuild_article_pagination();
rebuild_rss_feeds();
my ( $stdout, $stderr, $exit ) = capture {
    system(
        'ttree',  '-a',                      # process all files
        '-s',     'tmp',                     # use tmp/ as a source
        '-d',     '.',                       # use . as the target
        '--copy', '\.(gif|png|jpg|pdf)$',    # copy, don't process images
    )
};

if ( $stdout =~ /!.*file error/ ) {

    # yeah, ttree needs proper exit codes
    croak($stdout);
}
say $stdout;

system( 'prove', '-l', 't' );

sub preprocess_files (@files) {

    # scrub our tmp directory
    system( 'rm', '-fr', 'tmp' );
  FILE: foreach my $file (@files) {
        next FILE if $file =~ /\.sw\w$/;    # ignore vim swapfiles
        my $is_tt_file = $file =~ /\.tt$/;

        my $dir  = dirname($file);
        my $name = basename($file);

        if ( $dir !~ /static/ && !$is_tt_file ) {
            next FILE;
        }

        # make sure we have our destination directory
        my $destdir = $dir;
        $destdir =~ s/^root/tmp/;
        unless ( -d $destdir ) {
            mkpath($destdir);
        }

        my $destfile = catfile( $destdir, $name );

        if ( !$is_tt_file ) {

            # not a template toolkit file? Copy it over directly
            copy( $file, $destfile )
              or croak("Could not copy $file to $destfile: $!");
        }
        else {
            # ah, crap. We need to pre-process it.
            my $contents = slurp($file);

            if ( $is_tt_file && $contents =~ /^\s*\{\{TOC\}\}\s*$/m ) {
                $contents = build_toc( $file, $contents );
            }

            splat( $destfile, $contents );
        }
    }
}

sub build_toc ( $file, $contents ) {

    # XXX this is a simplistic heuristic. Need something better in the future.
    if ( $contents =~ m{\[%.*WRAPPER include/wrapper.*markdown=1.*%\]} ) {

        # we need to preprocess the markdown, replacing '#' headers with their
        # appropriate HTML tag. This allows the subsequent HTML parsing logic
        # to build the TOC, but still allows the bulk of Markdown processing
        # to happen in the templates, where it should be.
        my %replacement_for;
        while ( $contents =~ /^((#+)\s*(.*))$/mg ) {
            my $line  = $1;
            my $level = length($2);
            my $title = $3;
            my $h_tag = "<h$level>$title</h$level>";
            $replacement_for{$line} = $h_tag;
        }
        while ( my ( $line, $tag ) = each %replacement_for ) {
            $contents =~ s/\Q$line/$tag/;
        }
    }
    my $p = HTML::TokeParser::Simple->new( string => $contents );

    state $seen = {};

    my $rewritten = '';
    my @links;
    while ( my $token = $p->get_token ) {
        if ( $token->is_start_tag(qr/^h[1-6]$/i) ) {
            $rewritten .= $token->as_is;
            my $tag = $token->get_tag;
            $tag =~ /^h([1-6])$/i or croak("Bad 'h' tag in $file: $tag");
            my $level = $1;
            my $title = $p->peek(1);
            my $slug  = make_slug($title);
            if ( $seen->{$file}{$slug}++ ) {
                croak("Already seen heading '$title/$slug' in '$file'");
            }
            $rewritten .= qq{<a name="$slug"></a>};
            push @links =>
qq{    <li class="indent-$level"><a href="#$slug">$title</a></li>};
        }
        else {
            $rewritten .= $token->as_is;
        }
    }
    my $links = join "\n" => @links;
    my $toc   = <<"TOC";
<nav role="navigation" class="table-of-contents">
    <ul>
$links
    </ul>
</nav>
<hr>
TOC
    $rewritten =~ s/^s*\{\{TOC\}\}\s*$/$toc/m;
    return $rewritten;
}

sub rebuild_article_pagination () {
    foreach my $type (qw/article blog/) {
        my $article_type = article_type($type);
        my $pager        = Less::Pager->new( type => $type );
        while ( my $records = $pager->next ) {
            my $page_number = $pager->current_page_number;
            my $title       = "$article_type->{name} by Ovid";
            if ( $pager->total_pages > 1 ) {
                $title .= ", page $page_number";
            }
            my $articles   = get_article_list( $records, $article_type );
            my $pagination = get_pagination( $pager->total_pages, $page_number,
                $article_type );
            my $template = <<"END";
[%
    title      = '$title';
    identifier = 'articles';
%]

[% INCLUDE include/header.tt %]

$articles
$pagination
[% IF $page_number == 1 -%]
<script>
    var latestArticle  = document.getElementById("articles").firstElementChild.innerHTML;
    document.getElementById("articles").firstElementChild.innerHTML = '<em>' + latestArticle + '</em> <span class="new">New!</span>'
</script>
[%- END %]

[% INCLUDE include/footer.tt %]
END
            my $article = article_page( $page_number, $article_type );
            splat( "root/$article.tt", $template );
        }
    }
}

sub rebuild_rss_feeds () {
    my $base_url = 'https://ovid.github.io/';

    my $types =
      dbh()
      ->selectall_arrayref(
        'SELECT name, type, directory, description FROM article_types',
        { Slice => {} } );

    foreach my $type ( $types->@* ) {
        my $name      = $type->{name};
        my $directory = $type->{directory};
        my $now       = DateTime->now;
        my $year      = $now->year;
        my $rss       = XML::RSS->new( version => '2.0' );
        $rss->add_module(
            prefix => 'atom',
            uri    => 'http://www.w3.org/2005/Atom'
        );
        $rss->channel(
            title       => $type->{description},
            link        => "$base_url$directory",
            description => $type->{description},
            language    => 'en-us',
            copyright   => "Copyright $year, Curtis \"Ovid\" Poe",
            atom        => {
                'link' => {
                    'href' => "$base_url$type->{type}.rss",
                    'rel'  => 'self',
                    'type' => 'application/rss+xml'
                }
            },

            # RSS 2.0 requires RFC822 dates
            pubDate        => $now->strftime("%a, %d %b %Y %H:%M:%S %z"),
            managingEditor => 'curtis.poe@gmail.com (Curtis "Ovid" Poe)',
        );
        my $articles =
          dbh()->selectall_arrayref( <<"SQL", { Slice => {} }, $type->{type} );
    SELECT a.title,
           a.slug,
           a.description,
           a.created
      FROM articles a
      JOIN article_types at ON at.article_type_id = a.article_type_id
     WHERE available = 1 AND at.type = ?
  ORDER BY sort_order DESC
SQL
        foreach my $article ( $articles->@* ) {
            my $created =
              DateTime::Format::SQLite->parse_datetime( $article->{created} );
            $rss->add_item(
                title       => $article->{title},
                link        => "$base_url$directory/$article->{slug}.html",
                description => $article->{description},
                creator     => 'Curtis "Ovid" Poe',
                guid        => "$type->{type}/$article->{slug}",
                pubDate     => $created->strftime("%a, %d %b %Y %H:%M:%S %z"),
            );
        }
        splat( "$type->{type}.rss", $rss->as_string );
    }
}

sub get_pagination ( $total, $current, $article_type ) {
    return '' if $total == 1;
    my $pagination = qq{<nav class="pagination">\n};
    if ( $current > 1 ) {
        my $prev    = $current - 1;
        my $article = article_page( $prev, $article_type );
        $pagination .= qq{    <a href="/$article.html">&laquo;</a>\n};
    }
    else {
        $pagination .= qq{    <span class="inactive">&laquo;</span>\n};
    }
    for my $page ( 1 .. $total ) {
        my $class   = $page == $current ? 'class="active"' : '';
        my $article = article_page( $page, $article_type );
        $pagination .= qq{    <a $class href="/$article.html">$page</a>\n};
    }
    if ( $current < $total ) {
        my $next    = $current + 1;
        my $article = article_page( $next, $article_type );
        $pagination .= qq{    <a href="/$article.html">&raquo;</a>\n};
    }
    else {
        $pagination .= qq{    <span class="inactive">&raquo;</span>\n};
    }
    $pagination .= "</nav>";
    return $pagination;
}

sub article_page ( $number, $article_type ) {
    my $directory = $article_type->{directory};
    return 1 == $number ? $directory : "${directory}_$number";
}

sub get_article_list ( $records, $article_type ) {
    my $list = qq{<ul id="articles">\n};
    foreach my $article ( $records->@* ) {
        $list .=
qq{    <li><a href="/$article_type->{directory}/$article->{slug}.html">$article->{title}</a></li>\n};
    }
    $list .= "</ul>";
    return $list;
}

__END__

=head1 NAME

rebuild - Rebuilds site

=head1 DESCRIPTION

=over 4

=item * Preprocesses files (such as adding TOCs)

=item * Runs C<ttree>

=item * Runs all tests

=item * Rebuilds article pagination

=back
